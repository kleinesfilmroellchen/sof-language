# SOF test file - function tests

# define and run a function without arguments
{ 33 } 0 function test globaldef
test :

# define and run a function without arguments, check return value
{ 42 return } 0 function test globaldef
test : 42 = assert

# define and run a function with one argument, check return value
{ return } 1 function test globaldef
"return value" test : "return value" = assert

# define 2 arg function, check argument order is preserved and stack deleted
8888
   { return } 2 function test globaldef
   20 8 test : 8 = assert
8888 = assert

# first return encountered stops execution
{ true return false return } 0 function test globaldef
test : assert

# return nothing
true { false return:0 } 0 function test globaldef
test : assert
false { true return:0 true return } 0 function test globaldef
test : not assert

# although not done in the above tests, a function can be called directly
"string" { true return } 0 function . assert
"string" = assert

# call function within function
{ true test : return } 0 function . assert

# call codeblock within function
{ 2 { 3 + } . return } 0 function . 5 = assert

# run loop within function
{ 0 i def { { i . return } i . 5 > if } { i . 1 + dup i def 10 < } while return:0 } 0 function . 6 = assert

## Currying

{ + return } 2 function add globaldef
| 4 add : add4 globaldef
1 add4 : 5 = assert
-20 add4 15 = assert

# unnecessary currying delimiter
1 | 2 7 add : 9 = assert
   # ignored for anything else
   1 = assert

# no define after currying
| 80 add : 5 swap . 85 = assert

# argument order
{ pop return } 2 function discardOne globaldef
| "blah blah" discardOne : id globaldef
4 id : 4 = assert "not blah blah" id : "not blah blah" = assert

# The SOF programming language interpreter.
# Copyright (C) 2019-2020  kleinesfilmrÃ¶llchen

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
